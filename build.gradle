//Imports
import org.apache.tools.ant.filters.ReplaceTokens

//Repositories
buildscript {
    repositories {
        maven {
            url 'http://maven.ej-technologies.com/repository'
        }
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath 'com.github.edwgiz:maven-shade-plugin.log4j2-cachefile-transformer:2.6.1'
        classpath "com.github.jengelman.gradle.plugins:shadow:1.2.3"
    }
}

repositories {
    mavenCentral()
    jcenter()
}

//Plugins
apply plugin: 'java'
apply plugin: 'application'
apply plugin: "com.github.johnrengelman.shadow"

//Versions
def versionObj = new Version(major: 1, minor: 1, revision: "BETA")
group 'br.com.brjdevs.miyuki'
version versionObj.toString()

mainClassName = "br.com.brjdevs.miyuki.core.LoadController"

//Tasks
task sourcesForRelease(type: Copy) {
    from 'src/main/java'
    into 'build/filteredSrc'
    filter(ReplaceTokens, tokens: [
            rootVersionMajor: versionObj.major.toString(),
            rootVersionMinor: versionObj.minor.toString(),
            rootVersionRevision: versionObj.revision.toString(),
            rootVersionBuild: versionObj.build.toString()
    ])
}

compileJava {
    source = sourcesForRelease.destinationDir
    classpath = sourceSets.main.compileClasspath

    options.encoding = 'UTF-8'

    dependsOn sourcesForRelease
}

build {
    dependsOn clean
    dependsOn jar
    dependsOn shadowJar

    jar.mustRunAfter clean
    shadowJar.mustRunAfter jar
}

dependencies {
    compile project("Modules")
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.2'
}

artifacts {
    archives shadowJar
}

//Classes
class Version {
    String major, minor, revision

    String getBuild() {
        System.getenv("DRONE_BUILD_NUMBER") ?: System.getProperty("DRONE_BUILD_NUMBER") ?: "DEV"
    }

    String toString() {
        "${major}.${minor}.${revision}_$build"
    }
}